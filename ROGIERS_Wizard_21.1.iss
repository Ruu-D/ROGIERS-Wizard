; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ROGIERS Wizard"
#define MyAppVersion "21.1"
#define MyAppPublisher "Rogiers Vanpoucke NV/SA"
#define MyAppURL "https://www.rogiers.be/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B77129D4-C865-4229-84D7-35565C46CF4C}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\ROGIERS
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=ROGIERS Wizard 21.1
SetupIconFile=.\GRAPHICS\ROGIERS_LOGO.ico
AppCopyright=Daan Verhoost
DisableDirPage=yes
AllowRootDirectory=True
Uninstallable=no
ChangesEnvironment=True
WizardImageFile=.\GRAPHICS\ROGIERS_WELCOME.bmp
WizardSmallImageFile=.\GRAPHICS\InstallWizardSmall.bmp
DisableWelcomePage=False
;   "OutputDir" can change if compilation happens on another computer! All of the internal code references just follow the 'main folder'.
OutputDir=F:\MEGASYNC\ROGIERS-Wizard       
VersionInfoCompany=Rogiers Vanpoucke NV/SA
VersionInfoCopyright=Ⓒ Daan Verhoost
AlwaysRestart=True
InternalCompressLevel=ultra64
SolidCompression=True
Compression=lzma2/ultra64
WizardStyle=modern
VersionInfoVersion=21.1
MinVersion=0,6.1
ShowTasksTreeLines=True
UsePreviousSetupType=False
UsePreviousTasks=False
UsePreviousLanguage=False
ShowComponentSizes=False
WizardResizable=False

[Types]
Name: "Volledig"; Description: "Volledige installatie   (internet vereist)"Name: "Beperkt"; Description: "Essentiële bestanden"

[Components]
Name: "Volledig"; Description: "Volledig"; Types: Volledig; Flags: fixed
Name: "Beperkt"; Description: "Beperkt"; Types: Beperkt; Flags: fixed
Name: "Chocolatey"; Description: "ChocolateyPackageManager"; Types: Volledig

[Tasks]
Name: "ChangeWallpaper"; Description: "ROGIERS Wallpaper instellen"; GroupDescription: "Standaard :"; Components: Volledig Beperkt
Name: "InstallatieTotalCommander"; Description: "Installatie Total Commander [silent]"; GroupDescription: "Standaard :"; Components: Volledig Beperkt
Name: "ScriptsUitvoeren"; Description: "Powershell scripts uitvoeren [visuele effecten, snelkoppelingen maken, handleidingen kopiëren, ...]"; GroupDescription: "Standaard :"; Components: Volledig Beperkt
Name: "EnableWindowsRestorePoint"; Description: "Script uitvoeren om Windows herstelpunten te activeren"; GroupDescription: "Standaard :"; Components: Volledig Beperkt
Name: "ChocolateyApps"; Description: "Chocolatey : Apps installeren [Notepad++, Google Chrome, 7-Zip, CutePDF]"; GroupDescription: "Extra software :"; Components: Chocolatey
Name: "MapNetwerkDrives"; Description: "Bestand in Windows startup kopiëren om netwerkschijven te 'mappen'"; GroupDescription: "Voor softwaremensen :"; Flags: unchecked; Components: Volledig Beperkt

[Languages]
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"

[Files]
; DATA folder uit Installer kopieren naar main folder "C:\ROGIERS" :
Source: "DATA\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs onlyifdoesntexist; Permissions: everyone-full
; Scripts & files overschrijven indien al een bestaande installatie eerder werd uitgevoerd :
Source: "DATA\INSTALL\ROGIERS Installer\*"; DestDir: "{app}\INSTALL\ROGIERS Installer\"; Flags: ignoreversion recursesubdirs createallsubdirs; Permissions: everyone-full

; _______________________________________________________________________________________________________________________________
; Bestanden naar TEMP folder kopiëren :
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\00___WindowsDefenderExceptions.bat"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\08___ChangeWallpaperRogiers.ps1"; DestDir: "{tmp}"; Permissions: everyone-full

Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\01___TaskbarNeverCombine.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\02___ShowAllFileExtensions.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\03___LaunchExplorerToThisPC.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\04___HideTaskViewButton.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\12___DisableWindowAnimation.ps1"; DestDir: "{tmp}"; Permissions: everyone-full

Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\05___CreateRogiersAnyDeskShortcuts.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
;;;;;;;Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\06___CreateRogiersPDFManualShortcut.ps1"; DestDir: "{tmp}"; Permissions: everyone-full

Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\10___EnableWindowsRestorePoint.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\07___MapNetworkDrives.ps1"; DestDir: "{tmp}"; Permissions: everyone-full
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\11___TotalCommanderInstall.ps1"; DestDir: "{tmp}"; Permissions: everyone-full

; _______________________________________________________________________________________________________________________________
; Chocolatey packages :
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\09___ChocolateyInstallPackage.ps1"; DestDir: "{tmp}"; Permissions: everyone-full

; _______________________________________________________________________________________________________________________________
; Scripts voor wijziging van Windows taal en toetsenbord /// 'MyBeforeInstall' zorgt voor link naar [Code] waarin deze files na extract gebruikt worden :
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\13___1_ChangeLanguage_NL1.ps1"; DestDir: "{tmp}"; Permissions: everyone-full; BeforeInstall: BeforeInstall_NL1
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\13___2_ChangeLanguage_NL2.ps1"; DestDir: "{tmp}"; Permissions: everyone-full; BeforeInstall: BeforeInstall_NL2
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\13___3_ChangeLanguage_FR1.ps1"; DestDir: "{tmp}"; Permissions: everyone-full; BeforeInstall: BeforeInstall_FR1
Source: "DATA\INSTALL\ROGIERS Installer\SCRIPTS\13___4_ChangeLanguage_FR2.ps1"; DestDir: "{tmp}"; Permissions: everyone-full; BeforeInstall: BeforeInstall_FR2
; _______________________________________________________________________________________________________________________________

[Run]
; _______________________________________________________________________________________________________________________________

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\11___TotalCommanderInstall.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: waituntilterminated runhidden; \
    Components: Beperkt Volledig; \
    Tasks: InstallatieTotalCommander

; _______________________________________________________________________________________________________________________________

; Windows Defender uitzonderingen toevoegen :
Filename: "{app}\INSTALL\ROGIERS Installer\SCRIPTS\00___WindowsDefenderExceptions.bat"; \
    Parameters: "/verysilent"; \
    Flags: nowait; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

; ROGIERS Wallpaper instellen :
Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\08___ChangeWallpaperRogiers.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ChangeWallpaper

; Powershell scripts uitvoeren :
Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\01___TaskbarNeverCombine.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\02___ShowAllFileExtensions.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\03___LaunchExplorerToThisPC.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\04___HideTaskViewButton.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\12___DisableWindowAnimation.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\05___CreateRogiersAnyDeskShortcuts.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: ScriptsUitvoeren

;;;Filename: "powershell.exe"; \
;;;    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\06___CreateRogiersPDFManualShortcut.ps1"""; \
;;;    WorkingDir: "{app}"; \
;;;    Flags: runhidden; \
;;;;    Components: Beperkt Volledig; \
;;;    Tasks: ScriptsUitvoeren

; _______________________________________________________________________________________________________________________________

; Script voor uitvoeren van "Chocolatey" package manager :

Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\09___ChocolateyInstallPackage.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: waituntilterminated; \
    Components: Chocolatey; \

; _______________________________________________________________________________________________________________________________

; Bestand in Windows startup kopiëren om netwerkschijven te 'mappen' :
Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\07___MapNetworkDrives.ps1"""; \
    WorkingDir: "{app}"; \
    Flags: runhidden; \
    Components: Beperkt Volledig; \
    Tasks: MapNetwerkDrives

; Script uitvoeren om Windows herstelpunten te activeren :
Filename: "powershell.exe"; \
    Parameters: "-ExecutionPolicy Bypass -File ""{tmp}\10___EnableWindowsRestorePoint.ps1"""; \
    WorkingDir: "{app}"; \
    Components: Beperkt Volledig; \
    Tasks: EnableWindowsRestorePoint

; _______________________________________________________________________________________________________________________________

; Custom code (Pascal scripting)...

[ThirdParty]
CompileLogFile=.\DATA\INSTALL\ROGIERS Installer\Install_log.txt

; _______________________________________________________________________________________________________________________________

[Code]

var
  CustomQueryPage: TInputQueryWizardPage;
  CustomLanguagePage: TWizardPage;
  CheckListBox: TNewCheckListBox;
    NW: Integer;
    NL, FR: Integer;
    NL1, NL2: Integer;
    FR1, FR2: Integer;
ResultCode: Integer;
ErrorCode: Integer;

{_______________________________________________________________________________________________________________________________}

{ Uitlezen van datum voor verder gebruik in aanmaak van de mapnaam }
      function DateTime: String;
        begin
            Result := GetDateTimeString('yyyy/mm/dd', '.', ':');
        end;        

{ Ingave van gebruiker omzetten in 'String' om bij aanmaak van map opnieuw te gebruiken }
      function UserInput: String;
        begin
            Result := CustomQueryPage.Values[0];
        end;        

{_______________________________________________________________________________________________________________________________}
        
{ Procedure voor aanmaak nieuwe pagina }
procedure AddCustomQueryPage();
begin
  CustomQueryPage := CreateInputQueryPage(wpWelcome,
    'Naam technieker',
    'Vul hieronder de initialen van uw naam in (bv. DVH).',
    'Dit wordt gebruikt om een map te creëren in "C:\ROGIERS" op datum van vandaag. Vervolgens zal automatisch een map aangemaakt worden zoals : "2021.05.04 DVH". Plaats nadien hierin uw DAGRAPPORT en AFREGELBESTAND.');

{ Add items (False means it's not a password edit) }
  CustomQueryPage.Add('Initialen: ', False);
  end;

{_______________________________________________________________________________________________________________________________}

procedure InitializeWizard();
begin

{ Pagina na "wpWelcome" voor invullen van initialen = "CustomQueryPage" }
  AddCustomQueryPage();  

{ Pagina  na 'selectie van opdrachten' voor taalwijziging }
CustomLanguagePage:= CreateCustomPage(wpSelectTasks, 'Windows weergavetaal & toetsenbordindeling', 'Selecteer één van onderstaande opties.' + #13#10 + 'ENKEL mogelijk op Windows 10 "EYE-M" toestellen.');

  CheckListBox := TNewCheckListBox.Create(CustomLanguagePage);
  CheckListBox.Width := CustomLanguagePage.SurfaceWidth;
  CheckListBox.Height := ScaleY(118);
  CheckListBox.Flat := True;
  CheckListBox.Parent := CustomLanguagePage.Surface;

  NW := CheckListBox.AddRadioButton('Niet wijzigen', '', 0, True, True, nil);
  NL := CheckListBox.AddRadioButton('Windows taal wijzigen: NEDERLANDS', '', 0, False, True, nil);
        NL1 := CheckListBox.AddRadioButton('QWERTY toetsenbord (SCM origineel)', '', 1, False, True, nil);
        NL2 := CheckListBox.AddRadioButton('AZERTY toetsenbord', '', 1, False, True, nil);
  FR := CheckListBox.AddRadioButton('Windows taal wijzigen: FRANS', '', 0, False, True, nil);
        FR1 := CheckListBox.AddRadioButton('QWERTY toetsenbord (SCM origineel)', '', 1, False, True, nil);
        FR2 := CheckListBox.AddRadioButton('AZERTY toetsenbord', '', 1, False, True, nil);

end;

{_______________________________________________________________________________________________________________________________}

procedure BeforeInstall_NL1();   
{Execute Powershell script to modify Windows display language + keyboard layout, depending on which checkbox is selected on page "CustomLanguagePage"}      
begin
        if CheckListBox.Checked[NL1] then
        begin
        MsgBox('Taal instellingen worden nu gewijzigd naar:' + #13#10 + 'NEDERLANDS - QWERTY toetsenbord (SCM origineel)', mbInformation, MB_OK);
        Exec('powershell','-ExecutionPolicy Bypass "& ""C:\ROGIERS\INSTALL\ROGIERS Installer\SCRIPTS\13___1_ChangeLanguage_NL1.ps1"""', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);              
        end;
end;

procedure BeforeInstall_NL2();   
begin
        if CheckListBox.Checked[NL2] then
        begin
        MsgBox('Taal instellingen worden nu gewijzigd naar:' + #13#10 + 'NEDERLANDS - AZERTY toetsenbord', mbInformation, MB_OK);
        Exec('powershell','-ExecutionPolicy Bypass "& ""C:\ROGIERS\INSTALL\ROGIERS Installer\SCRIPTS\13___2_ChangeLanguage_NL2.ps1"""', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);              
        end;
end;

procedure BeforeInstall_FR1();   
begin
        if CheckListBox.Checked[FR1] then
        begin
        MsgBox('Taal instellingen worden nu gewijzigd naar:' + #13#10 + 'FRANS - QWERTY toetsenbord (SCM origineel)', mbInformation, MB_OK);
        Exec('powershell','-ExecutionPolicy Bypass "& ""C:\ROGIERS\INSTALL\ROGIERS Installer\SCRIPTS\13___3_ChangeLanguage_FR1.ps1"""', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);    
        end;
end;

procedure BeforeInstall_FR2();   
begin
        if CheckListBox.Checked[FR2] then
        begin
        MsgBox('Taal instellingen worden nu gewijzigd naar:' + #13#10 + 'FRANS - AZERTY toetsenbord', mbInformation, MB_OK);
        Exec('powershell','-ExecutionPolicy Bypass "& ""C:\ROGIERS\INSTALL\ROGIERS Installer\SCRIPTS\13___4_ChangeLanguage_FR2.ps1"""', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);    
        end;
end;


{_______________________________________________________________________________________________________________________________}

{ Procedure om input (naam) van gebruiker opnieuw te gebruiken voor aanmaak van map }
procedure CurStepChanged(CurStep: TSetupStep);

begin
   if CurStep = ssPostInstall then   
      begin
     { Map maken met : datum van vandaag + spatie + initialen van technieker (gebruiker ingave) }
     CreateDir('C:\ROGIERS\' + DateTime + ' ' + UserInput);
      end;

  end;
